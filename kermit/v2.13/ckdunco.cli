[!eq %/file%x,x]
    comment
    comment No switch was specified...may be trouble
    comment
     
    wr
    wr You need to specify a /file= switch to use the CLI macro.
    wr Usage: uncompress/file=filename [!ascii 333]-dfvc[!ascii 335] [!ascii 333]-b bits[!ascii 335]
    wr Execution proceeds anyway with i/o directed to stdin and stdout.
    wr
     
    x compress -d %-%
    
[!else]   
[!neq [!file [!ename %/file=%].Z]x,x]
    comment
    comment File does exist, so it is okay to proceed
    comment

    comment
    comment Verify the output file
    comment var0 = 0 means "run it"
    var0 0
    [!neq [!file [!ename %/file=%]]x,x]
        [!eq [!read Uncompressed file [!ename %/file=%] exists! Enter N to abort: ]x,NX]
            var0 1
            wr Please try again!
        [!end]
    [!end]
    [!eq [!var0],0]
        comment
        comment OKAY, now run it!
        comment
        wr
        wr Now uncompressing [!ascii 333][!enam %/file=%].Z[!ascii 335] to [!ascii 333][!enam %/file=%][!ascii 335]
        wr To compress, enter:  COMPRESS/FILE=%/file=%
        wr

        x compress/i=[!ename %/file=%].Z/o=[!ename %/file=%] -d %-%
    [!end]        
    
[!else]    
    wr
    wr The file [!ascii 333][!ename %/file=%].Z[!ascii 335] does not exist.  UNCOMPRESS will not be run!
    wr
[!end]    
[!end]    

